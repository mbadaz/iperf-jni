plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'org.jetbrains.dokka'
    id 'com.vanniktech.maven.publish'
}

android {
    compileSdk compile_sdk_version

    defaultConfig {
        minSdk min_sdk_version
        targetSdk compile_sdk_version
        ndk {
            abiFilters "arm64-v8a"
        }

        version = "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'

        externalNativeBuild {
            cmake {
                cFlags "-I./src/ -DHAVE_CONFIG_H -UHAVE_STDINT_H -UAF_INET6 -w -Wno-error=format-security"
                cppFlags "-I./src/ -std=c++14 -fexceptions -frtti -DANDROID_STL=c++_shared"
                arguments "-DANDROID_STL=c++_shared"
            }
        }
    }

    buildFeatures {
        buildConfig = true  // Explicitly enable BuildConfig to resolve deprecation warning
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            ndk {
                debugSymbolLevel "SYMBOL_TABLE"
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = '11'
    }

    externalNativeBuild {
        cmake {
            path file('src/main/jni/CMakeLists.txt')
            version "3.22.1"
        }
    }

    sourceSets {
        main {
            java {
                exclude 'com/synaptictools/iperf/StringList.java'
                exclude 'com/synaptictools/iperf/IntList.java'
                exclude 'com/synaptictools/iperf/ListDouble.java'
            }
        }
    }

    tasks.register('swigBuild') {
        exec {
            workingDir '.'
            commandLine 'sh', '-c', 'swig -c++ -java -package com.synaptictools.iperf -outdir src/main/java/com/synaptictools/iperf -o src/main/jni/iperf-jni-wrap.cpp src/main/jni/iperf-jni.i'
            ignoreExitValue true
        }
    }

    namespace 'com.synaptictools.iperf'
    ndkVersion '29.0.13846066 rc3'
}

tasks.register('sourcesJar', Jar) {
    group = 'jar'
    archiveClassifier = 'sources'  // NEW - correct property
    from android.sourceSets.main.java.srcDirs
}

tasks.register('javadoc', Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

tasks.register('javadocJar', Jar) {
    dependsOn dokkaJavadoc
    group = 'jar'
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation libraries
    implementation arch_libraries
    kapt librariesKapt
    testImplementation testLibraries
    androidTestImplementation androidTestLibraries
}

static def grepFile(file, grepPattern) {
    def lines = []
    file.eachLine {
        line -> if (line.matches(grepPattern)) {
            lines << line
        }
    }
    return lines
}

mavenPublish {
    androidVariantToPublish = "release"
}

tasks.register('generate_iperf_version_string_h') {
    //
    // retrive some string constants from configure.ac and
    // write them to iperf_version.txt
    //

    def configure_ac_path = 'iperf/src/main/jni/iperf3/configure.ac'
    def version_string_h_path = 'iperf/src/main/jni/iperf3/src/version.h'

    doLast {
        def lines
        def acinitLine
        def tokens

        // tokenize AC_INIT(...) to fetch version strings
        //
        // macro AC_INIT syntax is:
        //  AC_INIT (package, version, [bug-report],[tarname], [url])

        lines = grepFile(new File(configure_ac_path), ~/.*AC_INIT.*/)
        acinitLine = lines[0]
        tokens = acinitLine.split(/(\(|\)|,[ |\t]*)/)

        new File(version_string_h_path).withWriter { out ->

            out.println "#define PACKAGE_NAME        \"${tokens[1]}\""
            out.println "#define PACKAGE_VERSION     \"${tokens[2]}\""
            out.println "#define IPERF_VERSION       \"${tokens[2]}\""
            out.println "#define PACKAGE_STRING      \"${tokens[1]} ${tokens[2]}\""
            if (tokens.length > 3)
                out.println "#define PACKAGE_BUGREPORT   \"${tokens[3]}\""
            if (tokens.length > 4)
                out.println "#define PACKAGE_TARNAME     \"${tokens[4]}\""
            if (tokens.length > 5)
                out.println "#define PACKAGE_URL         \"${tokens[5]}\""
        }
    }
}

preBuild.dependsOn generate_iperf_version_string_h
preBuild.dependsOn swigBuild
